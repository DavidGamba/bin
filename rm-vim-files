#!/usr/bin/perl -w
use strict;
use Cwd qw(cwd abs_path);
use File::Find;
use Term::ANSIColor;
use Getopt::Long qw(:config passthrough);
use Pod::Usage;
my ( $case, $force, $file_pattern, $ignore_pattern, $swap );
GetOptions(
    'h|?'          => sub { pod2usage( -verbose => 1 ) },
    'help'         => sub { pod2usage( -verbose => 3 ) },
    'name|iname=s' => \$file_pattern,
    'v|ignore=s'   => \$ignore_pattern,
    'f|force'      => \$force,
    's|swap'       => \$swap,
);

my $path = shift;
$path = abs_path($path) if $path;
$path ||= cwd;

if($swap) { $file_pattern = qr/\.sw[po]$/; }
$file_pattern ||= qr/\.un~$/;

find( \&d, $path );

sub d
{
    my $file = $File::Find::name;
    return unless -f $file;
    return if $file_pattern and $file !~ /$file_pattern/i;
    return if $ignore_pattern and $file =~ /$ignore_pattern/i;
    if($force) {
        print "unlink ";
        unlink $file;
        print "$file\n";
    }
    else {
        print "[dry-run] rm $file\n";
    }
}

__END__

=pod

=head1 NAME

rm-vim-files

=head1 SYNOPSIS

    rm-vim-files [<location>] [--name|--iname <file pattern>] [-v|--ignore <file pattern>]
                 [-f|--force] [-s|--swap]

=head1 DESCRIPTION

removes undo files or files matches by C<< --name <file pattern> >>
recursively from current dir or B<location>. It also can filter out
results by C<< --ignore <file pattern> >>.

If B<< --force >> is not given then it just prints the results.
